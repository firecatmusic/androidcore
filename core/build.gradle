plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'maven-publish'
}

android {
    compileSdk 33

    defaultConfig {
        minSdk 23
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
        dataBinding true
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES']
        }
    }
    namespace 'com.crocodic.core'
}

//apply from: '../publish_local.gradle'

dependencies {

    /* Basic Android */
    api 'androidx.appcompat:appcompat:1.6.1'
    api 'androidx.constraintlayout:constraintlayout:2.1.4'
    api 'androidx.core:core-ktx:1.10.1'
    api 'androidx.exifinterface:exifinterface:1.3.6'
    api 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.1'
    api 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1'
    api 'androidx.security:security-crypto:1.1.0-alpha01'
    api 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

    /* Basic Component */
    api 'com.github.stfalcon-studio:StfalconImageViewer:1.0.1'
    api 'com.github.tapadoo:alerter:7.1.0'
    api 'com.jakewharton.timber:timber:5.0.1'
    api 'id.zelory:compressor:3.0.1'
    api 'org.greenrobot:eventbus:3.3.1'

    /* Glide */
    api 'com.github.bumptech.glide:glide:4.13.2'
    kapt 'com.github.bumptech.glide:compiler:4.13.2'
    api ('com.github.bumptech.glide:okhttp3-integration:4.12.0') {
        exclude group: 'glide-parent'
    }

    /* Basic Google */
    api 'com.google.android.material:material:1.9.0'
    api 'com.google.code.gson:gson:2.10.1'

    /* Location */
    api 'com.google.android.gms:play-services-location:21.0.1'
    api 'com.google.android.gms:play-services-maps:18.1.0'
    api 'com.google.maps.android:android-maps-utils:1.3.3'

    /* Networking */
    api 'com.squareup.retrofit2:retrofit:2.9.0'
    api 'com.squareup.retrofit2:converter-scalars:2.9.0'
    api 'com.squareup.retrofit2:converter-gson:2.9.0'
    api 'com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.2'

    /* Room */
    api 'androidx.room:room-ktx:2.5.1'
    kapt 'androidx.room:room-compiler:2.5.1'

    /* Firebase */
    api platform('com.google.firebase:firebase-bom:29.2.0')
    api ('com.google.firebase:firebase-analytics-ktx') {
        exclude module: 'play-services-ads-identifier'
        exclude module: 'play-services-measurement'
        exclude module: 'play-services-measurement-sdk'
    }
    api 'com.google.firebase:firebase-messaging-ktx'
    api 'com.google.firebase:firebase-crashlytics-ktx'
    api 'com.google.firebase:firebase-perf-ktx'

    /* Base64 Crypt */
    api group: 'commons-codec', name: 'commons-codec', version: '1.15'

    /* Paging3 */
    api 'androidx.paging:paging-runtime-ktx:3.1.1'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                from components.release
                groupId = 'com.github.crocodic-studio'
                artifactId = 'AndroidCoreProject'
                version = '4.0.10'
            }
        }
    }
}